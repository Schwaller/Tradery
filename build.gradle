// Root build.gradle for multi-module Tradery project

plugins {
    id 'org.beryx.jlink' version '3.0.1' apply false
    id 'org.beryx.runtime' version '1.13.1' apply false
    id 'org.gradlex.extra-java-module-info' version '1.9' apply false
}

allprojects {
    group = 'com.tradery'
    version = '1.0.1'

    repositories {
        mavenCentral()
        // JetBrains repo for pty4j dependencies
        maven { url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies' }
        // JitPack for JediTerm from GitHub
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.gradlex.extra-java-module-info'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
        // Enable module path inference for JPMS support
        modularity.inferModulePath = true
    }

    // Provide module info for JARs that don't have it
    extraJavaModuleInfo {
        failOnMissingModuleInfo = false

        // MessagePack libraries
        automaticModule("org.msgpack:msgpack-core", "msgpack.core")
        automaticModule("org.msgpack:jackson-dataformat-msgpack", "jackson.dataformat.msgpack")

        // Directory watcher
        automaticModule("io.methvin:directory-watcher", "directory.watcher")

        // Flexmark and common extensions
        automaticModule("com.vladsch.flexmark:flexmark", "com.vladsch.flexmark")
        automaticModule("com.vladsch.flexmark:flexmark-all", "com.vladsch.flexmark.all")
        automaticModule("com.vladsch.flexmark:flexmark-util", "com.vladsch.flexmark.util")
        automaticModule("com.vladsch.flexmark:flexmark-util-ast", "com.vladsch.flexmark.util.ast")
        automaticModule("com.vladsch.flexmark:flexmark-util-builder", "com.vladsch.flexmark.util.builder")
        automaticModule("com.vladsch.flexmark:flexmark-util-collection", "com.vladsch.flexmark.util.collection")
        automaticModule("com.vladsch.flexmark:flexmark-util-data", "com.vladsch.flexmark.util.data")
        automaticModule("com.vladsch.flexmark:flexmark-util-dependency", "com.vladsch.flexmark.util.dependency")
        automaticModule("com.vladsch.flexmark:flexmark-util-format", "com.vladsch.flexmark.util.format")
        automaticModule("com.vladsch.flexmark:flexmark-util-html", "com.vladsch.flexmark.util.html")
        automaticModule("com.vladsch.flexmark:flexmark-util-misc", "com.vladsch.flexmark.util.misc")
        automaticModule("com.vladsch.flexmark:flexmark-util-options", "com.vladsch.flexmark.util.options")
        automaticModule("com.vladsch.flexmark:flexmark-util-sequence", "com.vladsch.flexmark.util.sequence")
        automaticModule("com.vladsch.flexmark:flexmark-util-visitor", "com.vladsch.flexmark.util.visitor")
        automaticModule("com.vladsch.flexmark:flexmark-ext-tables", "com.vladsch.flexmark.ext.tables")
        automaticModule("com.vladsch.flexmark:flexmark-html-parser", "com.vladsch.flexmark.html.parser")

        // PTY4J - terminal support
        automaticModule("org.jetbrains.pty4j:pty4j", "pty4j")
        automaticModule("org.jetbrains.pty4j:purejavacomm", "purejavacomm")
    }

    dependencies {
        // Testing
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// License key generation task
// Usage: ./gradlew generateLicenseKey --args="365"
tasks.register('generateLicenseKey', JavaExec) {
    classpath = project(':tradery-license').sourceSets.main.runtimeClasspath
    mainClass = 'com.tradery.license.LicenseKeyGenerator'
}

// Shared dependency versions
ext {
    jacksonVersion = '2.16.0'
    okhttpVersion = '4.12.0'
    slf4jVersion = '2.0.9'
    log4jVersion = '2.25.3'
    sqliteVersion = '3.45.1.0'
    javalinVersion = '6.7.0'
    msgpackVersion = '0.9.6'
    websocketVersion = '1.5.6'
    jfreeChartVersion = '1.5.4'
    bouncyCastleVersion = '1.77'
}
